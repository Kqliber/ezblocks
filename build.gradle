import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'me.bristermitten.pdm' version '0.0.1'
    id 'application'
}

group 'me.clip'
version '2.0'

repositories {
    jcenter()
    mavenCentral()

    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }

    maven {
        url 'https://raw.githubusercontent.com/TeamVK/maven-repository/master/release/'
    }
}

dependencies {
    pdm 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.1-R0.1-SNAPSHOT'
    compile group: 'com.vk2gpz.tokenenchant', name: 'TokenEnchantAPI', version: '17.1.5'

    def exposedVersion = '0.24.1'
    pdm 'org.jetbrains.exposed:exposed-core:' + exposedVersion
    pdm 'org.jetbrains.exposed:exposed-dao:' + exposedVersion
    pdm 'org.jetbrains.exposed:exposed-jdbc:' + exposedVersion

    pdm 'com.zaxxer:HikariCP:3.4.5'
    pdm 'org.mariadb.jdbc:mariadb-java-client:2.6.0'
    pdm 'com.h2database:h2:1.4.200'
    pdm 'mysql:mysql-connector-java:8.0.20'
    pdm 'org.postgresql:postgresql:42.2.14'

    implementation 'me.mattstudios.utils:matt-framework:1.3'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    archiveFileName = "EZBlocks-${project.version}.jar"
}

jar.dependsOn project.tasks.getByName('pdm')